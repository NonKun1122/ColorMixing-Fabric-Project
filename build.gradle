plugins {
    id 'fabric-loom' version '1.7.4'
    id 'maven-publish'
}

version = mod_version
group = maven_group

base {
    archivesName = archives_base_name
}

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

/* FIX configuration cache problem */
tasks.named("processResources") {
    inputs.property("version", project.properties.version)
    inputs.property("minecraft_version", project.properties.minecraft_version)
    inputs.property("loader_version", project.properties.loader_version)

    filesMatching("fabric.mod.json") {
        expand([
                "version"          : project.properties.version,
                "minecraft_version": project.properties.minecraft_version,
                "loader_version"   : project.properties.loader_version
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}
